plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'org.spongepowered.plugin' version '0.9.0'
}

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://jitpack.io' }
}

sponge.plugin.id = pluginId
group = pluginGroup
version = pluginVersion

build.finalizedBy shadowJar
jar.enabled = false

shadowJar {
    classifier = null
}

dependencies {
    compileOnly 'org.spongepowered:spongeapi:7.2.0'
}

test {
    useJUnitPlatform()
}

tasks.create("publishBuildOnDiscord") {
    group = "Publishing"
    description = "Task for publishing the jar file to discord's jenkins channel"
    doLast {
        String[] jarFiles = new FileNameFinder().getFileNames(project.buildDir.getPath(), "**/*.jar")

        if(jarFiles.length > 0) {
            println("Found jar files: " + jarFiles)

            String lastCommitDescription = "git log -1 --pretty=%B".execute().getText()
            if(lastCommitDescription == null || lastCommitDescription == "") {
                lastCommitDescription = "No changelog provided"
            }

            exec {
                commandLine("java", "-jar", "..\\jenkinsdiscordbot-1.0.jar", "KillOrder", jarFiles[0], lastCommitDescription)
            }
        }
    }
}